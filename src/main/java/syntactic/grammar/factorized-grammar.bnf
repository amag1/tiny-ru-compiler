⟨program⟩ ::= ⟨Lista-Definiciones⟩ ⟨Start⟩
⟨Start⟩ ::= start ⟨Bloque-Método⟩
⟨Lista-Definiciones⟩ ::= ⟨Struct⟩ ⟨Lista-Definiciones⟩ | ⟨Impl⟩ ⟨Lista-Definiciones⟩ | λ
⟨Struct⟩ ::= struct idStruct ⟨Struct-O-Herencia⟩
⟨Struct-O-Herencia⟩ ::= ⟨Herencia⟩ { ⟨Struct-Atributo⟩ } | { ⟨Struct-Atributo⟩ }
⟨Struct-Atributo⟩ ::= ⟨Atributo⟩ ⟨Struct-Atributo⟩ | λ
⟨Atributo⟩ ::= ⟨Visibilidad⟩ ⟨Tipo⟩ ⟨Lista-Declaración-Variables⟩ ; | ⟨Tipo⟩ ⟨Lista-Declaración-Variables⟩ ;
⟨Impl⟩ ::= impl idStruct { ⟨Miembro⟩ ⟨Miembro-Opcional⟩ }
⟨Miembro-Opcional⟩ ::= ⟨Miembro⟩ ⟨Miembro-Opcional⟩ | λ
⟨Herencia⟩ ::= : ⟨Tipo⟩
⟨Miembro⟩ ::= ⟨Método⟩ | ⟨Constructor⟩
⟨Constructor⟩ ::= . ⟨Argumentos-Formales⟩ ⟨Bloque-Método⟩
⟨Método⟩ ::= ⟨Forma-Método⟩ fn idMetAt ⟨Argumentos-Formales⟩ -⟩ ⟨Tipo-Método⟩ ⟨Bloque-Método⟩ | fn idMetAt ⟨Argumentos-Formales⟩ -⟩ ⟨Tipo-Método⟩ ⟨Bloque-Método⟩
⟨Visibilidad⟩ ::= pri
⟨Forma-Método⟩ ::= st
⟨Bloque-Método⟩ ::= { ⟨Decl-Var-Locales-Metodo⟩ ⟨Sentencia-Metodo⟩ }
⟨Decl-Var-Locales-Metodo⟩ ::= ⟨Decl-Var-Locales⟩ ⟨Decl-Var-Locales-Metodo⟩ | λ
⟨Sentencia-Metodo⟩ ::= ⟨Sentencia⟩ ⟨Sentencia-Metodo⟩ | λ
⟨Decl-Var-Locales⟩ ::= ⟨Tipo⟩ ⟨Lista-Declaración-Variables⟩ ;
⟨Lista-Declaración-Variables⟩ ::= idMetAt ⟨Lambda-O-Variables⟩
⟨Lambda-O-Variables⟩ ::= λ | , ⟨Lista-Declaración-Variables⟩
⟨Argumentos-Formales⟩::= ( ) | ( ⟨Lista-Argumentos-Formales⟩ )
⟨Lista-Argumentos-Formales⟩ ::= ⟨Argumento-Formal⟩ ⟨Argumento-Formal-O-Lambda⟩
⟨Argumento-Formal⟩ ::= ⟨Tipo⟩ idMetAt
⟨Argumento-Formal-O-Lambda⟩ ::= , ⟨Lista-Argumentos-Formales⟩ | λ
⟨Tipo-Método⟩ ::= ⟨Tipo⟩ | void
⟨Tipo⟩ ::= ⟨Tipo-Primitivo⟩ | ⟨Tipo-Referencia⟩ | ⟨Tipo-Arreglo⟩
⟨Tipo-Primitivo⟩ ::= Str | Bool | Int | Char
⟨Tipo-Referencia⟩ ::= idStruct
⟨Tipo-Arreglo⟩ ::= Array ⟨Tipo-Primitivo⟩
⟨Sentencia⟩ ::= ret ⟨Expresion-O-Semicolon⟩ | if ( ⟨Expresión⟩ ) ⟨Sentencia⟩ ⟨Else-O-Lambda⟩ | ; | ⟨Asignación⟩ ; | ⟨Sentencia-Simple⟩ ; | while ( ⟨Expresión⟩ ) ⟨Sentencia⟩ | ⟨Bloque⟩
⟨Expresion-O-Semicolon⟩ ::= ⟨Expresión⟩ ; | ;
⟨Else-O-Lambda⟩ ::= λ | else ⟨Sentencia⟩
⟨Bloque⟩ ::= { ⟨Sentencia-Bloque⟩ }
⟨Sentencia-Bloque⟩ ::= ⟨Sentencia⟩ ⟨Sentencia-Bloque⟩ | λ
⟨Asignación⟩ ::= ⟨AccesoVar-Simple⟩ = ⟨Expresión⟩ | ⟨AccesoSelf-Simple⟩ = ⟨Expresión⟩
⟨AccesoVar-Simple⟩ ::= id ⟨Encadenado-O-Bracket⟩
⟨Encadenado-O-Bracket⟩ ::= ⟨Encadenado-Simple⟩ | [ ⟨Expresión⟩ ]
⟨Encadenado-Simple⟩ ::= . id ⟨Encadenado-Simple⟩ | λ
⟨AccesoSelf-Simple⟩ ::= self ⟨Encadenado-Simple⟩
⟨Sentencia-Simple⟩ ::= ( ⟨Expresión⟩ )
⟨Expresión⟩ ::= ⟨ExpOr⟩
⟨ExpOr⟩ ::= ⟨ExpAnd⟩ ⟨ExpOr`⟩
⟨ExpOr`⟩ ::= or ⟨ExpAnd⟩ ⟨ExpOr`⟩ | λ
⟨ExpAnd⟩ ::= ⟨ExpIgual⟩ ⟨ExpAnd`⟩
⟨ExpAnd`⟩ ::= && ⟨ExpIgual⟩ ⟨ExpAnd`⟩ | λ
⟨ExpIgual⟩ ::= ⟨ExpCompuesta⟩ ⟨ExpIgual`⟩
⟨ExpIgual`⟩ ::= ⟨OpIgual⟩ ⟨ExpCompuesta⟩ ⟨ExpIgual`⟩ | λ
⟨ExpCompuesta⟩ ::= ⟨ExpAd⟩ ⟨ExpCompuesta`⟩
⟨ExpCompuesta`⟩ ::= ⟨OpCompuesto⟩ ⟨ExpAd⟩ | λ
⟨ExpAd⟩ ::= ⟨ExpMul⟩ ⟨ExpAd`⟩
⟨ExpAd`⟩ ::= ⟨OpAd⟩ ⟨ExpMul⟩ ⟨ExpAd`⟩ | λ
⟨ExpMul⟩ ::= ⟨ExpUn⟩ ⟨ExpMul`⟩
⟨ExpMul`⟩ ::= ⟨OpMul⟩ ⟨ExpUn⟩ ⟨ExpMul`⟩ | λ
⟨ExpUn⟩ ::= ⟨OpUnario⟩ ⟨ExpUn⟩ | ⟨Operando⟩
⟨OpIgual⟩ ::= == | !=
⟨OpCompuesto⟩ ::= < | > | <= | >=
⟨OpAd⟩ ::= + | -
⟨OpUnario⟩ ::= + | - | ! | ++ | --
⟨OpMul⟩ ::= * | / | %
⟨Operando⟩ ::= ⟨Primario⟩ ⟨Primarios⟩ | ⟨Literal⟩
⟨Primarios⟩ ::= ⟨Encadenado⟩ | λ
⟨Literal⟩ ::= nil | true | false | intLiteral | StrLiteral | charLiteral
⟨Primario⟩ ::= ⟨ExpresionParentizada⟩ | ⟨AccesoSelf⟩ | ⟨AccesoVar⟩ | ⟨Llamada-Método⟩ | ⟨Llamada-Método-Estático⟩ | ⟨Llamada-Constructor⟩
⟨ExpresionParentizada⟩ ::= ( ⟨Expresion⟩ ) ⟨Encadenado-O-Lambda⟩
⟨Encadenado-O-Lambda⟩ ::= ⟨Encadenado⟩ | λ
⟨AccesoSelf⟩ ::= self ⟨Encadenado-O-Lambda⟩
⟨AccesoVar⟩ ::= id ⟨Encadenado-Expresion-Lambda⟩
⟨Encadenado-Expresion-Lambda⟩ ::= [ ⟨Expresión⟩ ] ⟨Encadenado-O-Lambda⟩ | ⟨Encadenado-O-Lambda⟩
⟨Llamada-Método⟩ ::= id ⟨Argumentos-Actuales⟩ ⟨Encadenado-O-Lambda⟩
⟨Llamada-Método-Estático⟩ ::= idStruct . ⟨Llamada-Método⟩ ⟨Encadenado-O-Lambda⟩
⟨Llamada-Constructor⟩ ::= new ⟨Llamada-New⟩
⟨Llamada-New⟩ ::= idStruct ⟨Argumentos-Actuales⟩ ⟨Encadenado-O-Lambda⟩ | ⟨Tipo-Primitivo⟩ [ ⟨Expresion⟩ ]
⟨Argumentos-Actuales⟩ ::= ( ⟨Abre-Par⟩
⟨Abre-Par⟩ ::= ⟨Lista-Expresiones⟩ ) | )
⟨Lista-Expresiones⟩ ::= ⟨Expresión⟩ ⟨Expresiones⟩
⟨Expresiones⟩ ::= λ | , ⟨Lista-Expresiones⟩
⟨Encadenado⟩ ::= . ⟨Encadenado-Punto⟩
⟨Encadenado-Punto⟩ ::= ⟨Llamada-Método-Encadenado⟩ | ⟨Acceso-Var⟩
⟨Llamada-Método-Encadenado⟩ ::= id ⟨Argumentos-Actuales⟩ ⟨Encadenado-O-Lambda⟩
